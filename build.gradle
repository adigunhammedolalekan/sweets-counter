// Top-level build file where you can add configuration options common to all sub-projects/modules.
import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

buildscript {

    ext.kotlin_version = '1.2.51'
    ext.objectboxVersion = '2.0.0-beta'
    ext.gradleVersion = '3.2.0-beta02'
    ext.navigationVersion = '1.0.0-alpha02'

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$gradleVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "io.objectbox:objectbox-gradle-plugin:$objectboxVersion"
        classpath 'com.frogermcs.androiddevmetrics:androiddevmetrics-plugin:0.6'
        classpath "android.arch.navigation:navigation-safe-args-gradle-plugin:$navigationVersion"
        classpath "com.vanniktech:gradle-dependency-graph-generator-plugin:0.5.0"
    }
}

apply plugin: "com.vanniktech.dependency.graph.generator"
apply from: 'dependencies.gradle'

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "http://objectbox.net/beta-repo/" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        maven { url 'https://jitpack.io' }
    }

    configurations {
        ktlint
    }

    dependencies {
        ktlint "com.github.shyiko:ktlint:0.24.0"
    }

    // https://github.com/google/dagger/issues/306
    afterEvaluate {
        tasks.withType(JavaCompile.class) {
            options.compilerArgs << "-Xmaxerrs" << "500"
        }
    }

    tasks.withType(Test) {
        testLogging {
            events TestLogEvent.FAILED,
                    TestLogEvent.PASSED,
                    TestLogEvent.SKIPPED,
                    TestLogEvent.STANDARD_ERROR,
                    TestLogEvent.STANDARD_OUT
            exceptionFormat TestExceptionFormat.FULL
            showCauses true
            showExceptions true
            showStackTraces true
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}